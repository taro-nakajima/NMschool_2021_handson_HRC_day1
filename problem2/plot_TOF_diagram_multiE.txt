# A gnuplot script to draw a TOF diagram
#
# ============= user variables ===============================
# The energy of the incident neutron beam. (meV)
Ei=160.0

# Excitation energy (meV)
E=25.0

# Frequency of the Fermi chopper (Hz)
f=200.0

# Velocity for the incident neutrons  (m/ms)
vi=0.437*sqrt(Ei)

# Velocity of the scattered neutrons (m/ms)
vf=0.437*sqrt(Ei-E)

# Maximum of the horizontal (TOF) axis (ms)
TOF_max=20.0

# ========= please do not change following commands =============
# Distance between the neutron source and the sample position (m)
L1=15.0

# Distance between the sample position and the detectors (m)
L2=5.0

# Distance between the sample position and the Fermi chopper (m)
L3=1.03

# The time at which the incident neutron arrives at the sample position (ms)
ts = L1/vi

# The time at which the incident neutron arrives at the sample position (ms)
tc = (L1-L3)/vi

# The times at which the 2nd, 3rd incident neutrons arrive at the sample position (ms)
tc2 = tc+1/f*1000.0
tc3 = tc+2/f*1000.0

# The velocity of the 2nd, 3rd incident neutron beam. (m/ms)
vi2 = (L1-L3)/tc2
vi3 = (L1-L3)/tc3

# The energies of the 2nd, 3rd incident neutron beams. (meV)
Ei2=(vi2/0.437)**2.0
Ei3=(vi3/0.437)**2.0

# The energies of the 2nd, 3rd scattered neutron beams. (meV)
vf2=0.437*sqrt(Ei2-E)
vf3=0.437*sqrt(Ei3-E)

# The time at which the 2nd 3rd incident neutron beams arrive at the sample position (ms)
ts2 = L1/vi2
ts3 = L1/vi3


set title sprintf('Time of flight diagram at HRC, E_i=%4.1f meV, E=%4.1f meV',Ei,E) font "Arial,14" 
set xlabel 'Time of flight (ms)' font "Arial,16" 
set ylabel 'Distance from the source (m)' font "Arial,16" 

set xrange [0:TOF_max]
set yrange [0:L1+L2]

TOFline_Ei_1(x)=vi*x  # 1st Ei
TOFline_Ei_2(x)=vi2*x  # 2nd Ei
TOFline_Ei_3(x)=vi3*x  # 3rd Ei

TOFline_Ef_1(x)=(x < ts ? vi*x : vf*x + (L1-(vf*ts)))
TOFline_Ef_2(x)=(x < ts2 ? vi2*x : vf2*x + (L1-(vf2*ts2)))
TOFline_Ef_3(x)=(x < ts3 ? vi3*x : vf3*x + (L1-(vf3*ts3)))

SamplePos(x)=L1
DetPos(x)=L1+L2
Fermi(x)=L1-L3

#for png
set samples 1000
set term png size 800, 500
set size ratio 0.9  # ratio r = height/width
set out 'HRC_TOF_diagram_02.png'
set key right bottom

set style line 1 lt 1 lc "#ff0000" lw 2 pt 7 ps 1.5
set style line 2 lt 1 lc "#8080FF" lw 2 pt 7 ps 1.5
set style line 3 lt 1 lc "#00C000" lw 1 pt 7 ps 1.5
set style line 11 lt 1 lc "#000000" lw 1 pt 6 ps 1.5

set label 1 at first  TOF_max,L1 " Sample"
set label 2 at first  TOF_max,(L1+L2) " Detector"
set label 3 at first  TOF_max,(L1-L3) " Fermi Chopper"

plot SamplePos(x) ls 3 not,\
DetPos(x) ls 3 not,\
Fermi(x) ls 3 not,\
TOFline_Ef_1(x) ls 2 t sprintf('inelastic (E=%4.1f)',E),\
TOFline_Ef_2(x) ls 2 not,\
TOFline_Ef_2(x) ls 2 not,\
TOFline_Ei_1(x) ls 1 t sprintf('elastic (E_i(1)=%4.1f)',Ei),\
TOFline_Ei_2(x) ls 1 t sprintf('elastic (E_i(2)=%4.1f)',Ei2),\
TOFline_Ei_3(x) ls 1 t sprintf('elastic (E_i(3)=%4.1f)',Ei3)

set out